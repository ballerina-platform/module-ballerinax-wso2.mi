import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'java'
    id "de.undercouch.download" version "5.4.0"
}

group = project.group
version = project.version

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.ballerinalang:ballerina-lang:${ballerinaLangVersion}"
    implementation "org.ballerinalang:ballerina-cli:${ballerinaLangVersion}"
    implementation "org.apache.ws.commons.axiom:axiom-api:${axiomVersion}"
    implementation "org.wso2.carbon.module:module-core:${carbonModuleCoreVersion}"
    implementation "org.apache.axis2.wso2:axis2:${axis2Version}"

    testImplementation 'org.testng:testng:7.7.0'
}

def projects = ['project2', 'project3']

task buildBallerinaProjects {
    doLast {
        def command = projects.collect { "bal build src/test/resources/ballerina/$it" }.join(' && ')
        exec {
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine 'cmd', "/c", command
            } else {
                commandLine 'sh', "-c", command
            }
        }
    }
}

test {
    systemProperty "ballerina.home", "build/ballerina/ballerina-${ballerinaLangVersion}-swan-lake/" +
            "distributions/ballerina-${ballerinaLangVersion}"
    useTestNG() {
        suites 'src/test/resources/testng.xml'
    }
    dependencies {
        implementation fileTree(dir: 'src/test/resources/libs', include: '*.jar')
    }
}

task downloadBallerina(type: Download) {
    src([
            "https://dist.ballerina.io/downloads/${ballerinaLangVersion}/" +
                    "ballerina-${ballerinaLangVersion}-swan-lake.zip"
    ])
    dest file("build")
    overwrite false
}

task unzipFile(type: Copy) {
    dependsOn("downloadBallerina")
    from(zipTree("build/ballerina-${ballerinaLangVersion}-swan-lake.zip"))
    into("build/ballerina")
}

task copyBallerinaJars(type: Copy) {
    dependsOn("buildBallerinaProjects")
    projects.each { project ->
        from("src/test/resources/ballerina/${project}/target/bin/${project}.jar")
        into('src/test/resources/libs')
    }
}
compileTestJava.dependsOn("copyBallerinaJars")
processTestResources.dependsOn("copyBallerinaJars")

test.dependsOn(":mi-ballerina:localPublish")
test.dependsOn("unzipFile")
test.dependsOn("copyBallerinaJars")

task deleteFiles(type: Delete) {
    doLast {
        projects.each { project ->
            delete("src/test/resources/libs/${project}.jar")
            delete("src/test/resources/ballerina/${project}/target")
        }
    }
}

clean.dependsOn("deleteFiles")
